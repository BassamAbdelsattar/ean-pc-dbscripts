USE [eanprod]
GO

/****** Object:  StoredProcedure [dbo].[spOneToRuleThemAll]    Script Date: 15-1-2015 23:03:27 ******/
DROP PROCEDURE [dbo].[spOneToRuleThemAll]
GO

/****** Object:  StoredProcedure [dbo].[spOneToRuleThemAll]    Script Date: 15-1-2015 23:03:27 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[spOneToRuleThemAll] @fromFile nvarchar(1000), @formatFile nvarchar(1000), @targetTable nvarchar(100)
AS
SET ANSI_WARNINGS OFF
Declare @TempSQL nvarchar(max)
Declare @MergeSQL nvarchar(max)
Declare @UpdateSQL nvarchar(max)
Declare @InsertSQL nvarchar(max)
Declare @DeleteSQL nvarchar(max)
Declare @PK nvarchar(100)
 
--Getting PrimaryKey
set @TempSQL = 'select @PK = b.COLUMN_NAME from [eanprod].information_schema.TABLE_CONSTRAINTS a '+
 'JOIN [eanprod].information_schema.CONSTRAINT_COLUMN_USAGE b on a.CONSTRAINT_NAME=b.CONSTRAINT_NAME '+
  'where a.CONSTRAINT_SCHEMA=''dbo'' and a.TABLE_NAME = '''+@targetTable+''' and a.CONSTRAINT_TYPE = ''PRIMARY KEY'''

exec sp_executesql @TempSQL, N'@PK nvarchar(100) out', @PK out
Select @PK

--Select Part
SET @MergeSQL =	'MERGE '+@targetTable+' AS Target ' +
			'USING (SELECT * FROM OPENROWSET(BULK ''' +@fromFile+ ''', FORMATFILE = '''+@formatFile+''', FIRSTROW = 2) AS BCP) AS Source' +
			' ON Target.'+@PK+' = Source.'+@PK+''

--Update Part
SELECT @UpdateSQL = ' WHEN MATCHED THEN UPDATE SET '
SELECT @UpdateSQL = @UpdateSQL + 'Target.'+COLUMN_NAME+' = Source.'+COLUMN_NAME+',' 
FROM INFORMATION_SCHEMA.COLUMNS
WHERE TABLE_NAME = ''+@targetTable+'' AND TABLE_SCHEMA='dbo' and COLUMN_NAME != 'Timestamp'

select @UpdateSQL = SUBSTRING(@UpdateSQL,1,len(@UpdateSQL)-1)

--Insert Part
SELECT @InsertSQL = ' WHEN NOT MATCHED BY Target THEN INSERT('
SELECT @InsertSQL = @InsertSQL + COLUMN_NAME+',' 
FROM INFORMATION_SCHEMA.COLUMNS
WHERE TABLE_NAME = ''+@targetTable+'' AND TABLE_SCHEMA='dbo' and COLUMN_NAME != 'Timestamp'
select @InsertSQL = SUBSTRING(@InsertSQL,1,len(@InsertSQL)-1) +')'

SELECT @InsertSQL = @InsertSQL + 'VALUES('
SELECT @InsertSQL = @InsertSQL + 'source.'+COLUMN_NAME+',' 
FROM INFORMATION_SCHEMA.COLUMNS
WHERE TABLE_NAME = ''+@targetTable+'' AND TABLE_SCHEMA='dbo' and COLUMN_NAME != 'Timestamp'
select @InsertSQL = SUBSTRING(@InsertSQL,1,len(@InsertSQL)-1) +')'

-- DELETE RECORD
select @DeleteSQL = 'WHEN NOT MATCHED BY Source THEN DELETE'

select @TempSQL = @MergeSQL + @UpdateSQL +@InsertSQL + @DeleteSQL +';'

exec(@TempSQL)

-- turn WARNINGS back on						
SET ANSI_WARNINGS ON

GO


